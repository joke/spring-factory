plugins {
    id 'idea'
    id 'info.solidsoft.pitest' version '1.6.0'
    id 'io.freefair.git-version' version '6.1.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '2.2.1'
}

allprojects { project ->
    group 'io.github.joke.spring-factory'
    description 'annotation processor for spring factories'

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaBasePlugin) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    plugins.withType(MavenPublishPlugin) {
        project.plugins.apply 'signing'

        plugins.withType(JavaPlugin) {
            java {
                withJavadocJar()
                withSourcesJar()
            }
        }

        plugins.withType(SigningPlugin) {
            afterEvaluate {
                signing {
                    required {
                        gradle.taskGraph.hasTask('publish')
                    }
                    useGpgCmd()
                    sign publishing.publications
                }
            }
        }

        plugins.withType(JacocoPlugin) {
            check {
                dependsOn jacocoTestCoverageVerification
            }
            test {
                finalizedBy jacocoTestReport
            }
            jacocoTestReport {
                dependsOn test
                reports {
                    xml.enabled true
                    html.enabled true
                }
            }
            jacocoTestCoverageVerification {
                violationRules {
                    rule {
                        limit {
                            minimum = 0.1
                        }
                    }
                }
            }
        }

        publishing {
            repositories {
                maven {
                    name = 'OSSRH'
                    url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials(PasswordCredentials)
                }
                maven {
                    name = 'GitHubPackages'
                    url = uri("https://maven.pkg.github.com/joke/${project.name}")
                    credentials(PasswordCredentials)
                }
            }
            afterEvaluate {
                tasks.withType(AbstractPublishToMaven)*.dependsOn(check)
                publications.each { publication ->
                    publication.pom {
                        name = project.name
                        description = project.description
                        url = "https://github.com/joke/${project.name}"
                        licenses {
                            license {
                                name = 'Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }
                        developers {
                            developer {
                                id = 'joke'
                                name = 'Joke de Buhr'
                                email = 'joke@seiken.de'
                            }
                        }
                        scm {
                            connection = "scm:git:git://github.com/joke/${project.name}.git"
                            developerConnection = "scm:git:git://github.com/joke/${project.name}.git"
                            url = "http://github.com/joke/${project.name}/tree/master"
                        }
                    }
                }
            }
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.6.1'
}